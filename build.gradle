plugins {
    id 'java'
    id 'maven-publish'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version "9.1.0"
    id("com.modrinth.minotaur") version "2.8.7"
}

def getLatestGitTag() {
    try {
        providers.exec {
            commandLine("git", "describe", "--tags", "--abbrev=0")
        }.standardOutput.asText.get().trim()
    } catch (Exception ignore) {
        "unknown"
    }
}

def tag = System.getenv("TAG") ?: getLatestGitTag()
def isSnapshot = System.getenv('SNAPSHOT')?.toBoolean() ?: false
def gitHash = System.getenv("GIT_HASH") ?: "local"

group = 'me.axeno'
version = isSnapshot ? "${tag}-${gitHash.take(7)}" : (System.getenv("TAG") ? tag : "$tag-local")

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:${minecraft_version}-R0.1-SNAPSHOT")

    implementation("io.github.revxrsal:lamp.common:${revxrsal_lamp_version}")
    implementation("io.github.revxrsal:lamp.brigadier:${revxrsal_lamp_version}")
    implementation("io.github.revxrsal:lamp.bukkit:${revxrsal_lamp_version}")

    implementation("com.j256.ormlite:ormlite-jdbc:${ormlite_version}")
    implementation("org.xerial:sqlite-jdbc:${sqlite_jdbc_version}")
    implementation("com.mysql:mysql-connector-j:${mysql_connector_version}")

    compileOnly("org.projectlombok:lombok:${lombok_version}")
    annotationProcessor("org.projectlombok:lombok:${lombok_version}")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.10.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.10.1")
    testImplementation("org.mockito:mockito-core:5.8.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.8.0")
    testImplementation("io.papermc.paper:paper-api:${minecraft_version}-R0.1-SNAPSHOT")

    testImplementation("com.j256.ormlite:ormlite-jdbc:${ormlite_version}")
    testImplementation("org.xerial:sqlite-jdbc:${sqlite_jdbc_version}")

    testCompileOnly("org.projectlombok:lombok:${lombok_version}")
    testAnnotationProcessor("org.projectlombok:lombok:${lombok_version}")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("${minecraft_version}")
    }

    test {
        useJUnitPlatform()
        testLogging {
            events("passed", "skipped", "failed")
            exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
            showStandardStreams = false
        }
    }
}

def targetJavaVersion = 21
java {
    withSourcesJar()
    withJavadocJar()

    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    def props = [version: version, minecraft_version: minecraft_version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "hommr"
    versionNumber = version
    versionName = "Hommr ${version}"
    versionType = version.toString().contains("beta") ? "beta" : (version.toString().contains("alpha") ? "alpha" : "release")
    uploadFile = tasks.shadowJar
    gameVersions = ["${minecraft_version}"]
    loaders = ["paper", "purpur"]
    syncBodyFrom = file("README.md").text
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId = 'hommr'

            pom {
                name = 'Hommr'
                description = 'Easily set, teleport, and manage your homes.'
                url = 'https://github.com/AxenoDev/Hommr'

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://opensource.org/licenses/GPL-3.0'
                    }
                }

                developers {
                    developer {
                        id = 'AxenoDev'
                        name = 'AxenoDev'
                        url = 'https://axeno.me'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/AxenoDev/Hommr.git'
                    developerConnection = 'scm:git:ssh://github.com:AxenoDev/Hommr.git'
                    url = 'https://github.com/AxenoDev/Hommr'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/AxenoDev/Hommr")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}